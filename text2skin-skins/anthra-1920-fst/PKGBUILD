pkgbase=anthra-1920-FSt
pkgname=text2skin-$(echo $pkgbase | tr '[:upper:]' '[:lower:]')
pkgver=20140324
pkgrel=2
pkgdesc="Dark, anthracite skins for Klaus Schmidingers VDR"
arch=('any')
url="http://www.anthra.de/"
depends=('vdr-text2skin')
optdepends=('anthra-logos: To show channellogos in DisplayChannel view')
groups=('text2skin-anthra')
license=('GPL2')
install=$pkgname.install
source=("http://www.anthra.de/anthra-skins/${pkgbase//-/_}-$pkgver.tar.xz")
md5sums=('7e0f0d85faf393eb70daeaa6dbc6c288')

prepare() {
  cd "$srcdir/${pkgbase//-/_}"
  sed -i 's/var\/lib\/vdr/usr\/share\/vdr\/plugins\/text2skin/g' ${pkgbase//-/_}.skin
}

package() {
  cd "$srcdir/${pkgbase//-/_}"
  make DESTDIR="$pkgdir" THEMESDIR="/var/lib/vdr/themes" install
  find "$pkgdir" -name COPYING -delete
  find "$pkgdir" -name .directory -delete
}
