# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
# Contributor: Ole Ernst <olebowle[at]gmx[dot]com>
pkgbase=drivers
pkgname=('digitaldevices-dvb-drivers' 'dvbsky-dvb-drivers' 'technotrend-dvb-drivers')
pkgver=3.14.1
pkgrel=2
_extramodules=extramodules-3.14-ARCH
_saa716xrev=196681f1e154
_media_buildrev=04cd0952eb04
url='https://github.com/CReimer/vdr4arch'
pkgdesc="Additional or updated dvb driver modules"
arch=('x86_64' 'i686')
license=('GPL2')
depends=("linux=$pkgver")
makedepends=('mercurial' "linux-headers=$pkgver")
install="${pkgbase}.install"
source=("https://www.kernel.org/pub/linux/kernel/v3.x/linux-$pkgver.tar.xz"
        'kernel-3.14-dvbsky.patch'
        'http://www.dvbsky.net/download/linux/media_build-bst-140128.tar.gz'
        'sit2.patch'
        '010_dd-frontends-kconfig.diff'
        '010_dd-frontends-kconfig2.diff'
        '010_dd-frontends-makefile.diff'
        'backport-dddvb.diff'
        "hg+https://bitbucket.org/powARman/v4l-dvb-saa716x#revision=$_saa716xrev"
        "hg+http://linuxtv.org/hg/~endriss/media_build_experimental#revision=$_media_buildrev")
md5sums=('f53082540eed294f486a43fc67646ed2'
         '78e2d47dcf43bbb1c06151f8e00831eb'
         'c538c1635e7f9838f1c56409943f7080'
         'e69319bb53b84ed2ea5848f028e8eccf'
         '485eea60abca891ce6cb291bb7f971de'
         'dcb9acaa0496cd430855c0defd2dcf5a'
         'd38c96f0d295fa39e81c51e792c49ba4'
         '9cbe72fa2c58bd84431d20d6a90d37d7'
         'SKIP'
         'SKIP')

#dvbsky server doesn't like curl's user agent
DLAGENTS="$( IFS=$'\n'; echo "${DLAGENTS[*]}" | grep '^http::' ) --user-agent 'Mozilla/4.0'"

prepare() {
  if ! grep -q "EXTRAMODULES=$_extramodules" "$startdir/$pkgbase.install"; then
    sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${pkgbase}.install"
  fi


  #dd
  cd "$srcdir/media_build_experimental/experimental/dddvb"
  patch -p1 -i "$srcdir/backport-dddvb.diff"
  cp frontends/{stv0367dd*,tda18212dd*,cxd2843*,lnbh25*,stv6111*,stv0910*} $srcdir/linux-$pkgver/drivers/media/dvb-frontends/
  install -Dm644 include/linux/dvb/mod.h $srcdir/linux-$pkgver/include/linux/dvb/mod.h

  rm "$srcdir/linux-$pkgver/drivers/media/pci/ddbridge/"{*.c,*.h,Makefile}
  cp -r ddbridge "$srcdir/linux-$pkgver/drivers/media/pci/"

  cd "$srcdir/linux-$pkgver"
#   patch -p1 -i "$srcdir/media_build_experimental/experimental/patch.d/010_dd-frontends-kconfig.diff"
  patch -p1 -i "$srcdir/010_dd-frontends-kconfig.diff"
  patch -p1 -i "$srcdir/010_dd-frontends-kconfig2.diff"
  patch -p1 -i "$srcdir/010_dd-frontends-makefile.diff"
  patch -p1 -i "$srcdir/media_build_experimental/experimental/patch.d/020_ddbridge-drxk.diff"
  patch -p1 -i "$srcdir/media_build_experimental/experimental/patch.d/100_dvbdev.h.diff"
  patch -p1 -i "$srcdir/media_build_experimental/experimental/patch.d/150_cxd2843_deadlock_fix.diff"


  #dvbsky
  cd "$srcdir/linux-$pkgver"
   ln -sf "$srcdir"/media_build-bst/v4l/sit2.o.x${CARCH: -2} drivers/media/dvb-frontends/sit2.o
   patch -Np1 -i "$srcdir/kernel-3.14-dvbsky.patch"
   patch -Np1 -i "$srcdir/sit2.patch"


  #technotrend
  echo 'EXTRA_CFLAGS += -Idrivers/media/tuners' >> "$srcdir/v4l-dvb-saa716x/linux/drivers/media/common/saa716x/Makefile"
  cd "$srcdir"/linux-$pkgver
  patch -p1 -i "$srcdir/media_build_experimental/experimental/saa716x.include-osd.diff"
  patch -p1 -i "$srcdir/media_build_experimental/experimental/saa716x.pci-kconfig.diff"
  patch -p1 -i "$srcdir/media_build_experimental/experimental/saa716x.pci-makefile.diff"
  patch -p1 -i "$srcdir/media_build_experimental/experimental/patch.d/320_stv090x_ucblocks_ber.diff"
  cp -r "$srcdir/v4l-dvb-saa716x/linux/drivers/media/common/saa716x" drivers/media/pci


  #Prepare full kernel source. linux-headers package is incomplete.
  cd "$srcdir/linux-$pkgver"
  cp /usr/lib/modules/$pkgver*-ARCH/build/.config .
  cp /usr/lib/modules/$pkgver*-ARCH/build/Module.symvers .
  echo CONFIG_SAA716X_SUPPORT=y >> .config
  yes 'm' | make oldconfig
  make prepare
  make scripts
}

build() {
  for _folder in dvb-frontends \
                 pci/cx23885 \
                 pci/cx88 \
                 pci/ddbridge \
                 pci/saa716x \
                 rc/keymaps \
                 usb/dvb-usb-v2; do
    cd "$srcdir/linux-$pkgver/drivers/media/$_folder"
    make -C "$srcdir"/linux-$pkgver M=$(pwd) modules
  done
}

package_digitaldevices-dvb-drivers() {
  pkgdesc+="(Digital Devices/Linux4Media)"
  replaces=('dddvb' 'ngene-octopus-test')
  conflicts=('dddvb' 'ngene-octopus-test')
  mkdir -p "$pkgdir"/usr/lib/modules/$_extramodules
  cd "$pkgdir"/usr/lib/modules/$_extramodules
  mv "$srcdir"/linux-$pkgver/drivers/media/pci/ddbridge/ddbridge.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/stv0367dd.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/tda18212dd.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/cxd2843.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/lnbh25.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/stv6111.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/stv0910.ko .
  gzip -9 *
}

package_dvbsky-dvb-drivers() {
  pkgdesc+="(DVBSky)"
  replaces=('media-build-dvbsky')
  conflicts=('media-build-dvbsky')
  license=('unknown') #sit2.o is obviously closed source.
  mkdir -p "$pkgdir"/usr/lib/modules/$_extramodules
  cd "$pkgdir"/usr/lib/modules/$_extramodules
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/m88d{c2800,s3103_dvbsky}.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/sit2fe.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/pci/cx23885/cx23885.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/pci/cx88/cx88{-dvb,00,xx}.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/rc/keymaps/rc-dvbsky.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/usb/dvb-usb-v2/dvb-usb-dvbsky.ko .
  gzip -9 *
}

package_technotrend-dvb-drivers() {
  pkgdesc+="(TechnoTrend)"
  replaces=('v4l-dvb-saa716x')
  conflicts=('v4l-dvb-saa716x')
  mkdir -p "$pkgdir"/usr/lib/modules/$_extramodules
  cd "$pkgdir"/usr/lib/modules/$_extramodules
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/stv090x.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/pci/saa716x/*.ko .
  gzip -9 *
}
