# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-skinflatplus
pkgver=0.4.1
_vdrapi=2.1.6
pkgrel=2
pkgdesc="Simple and slim skin for VDR"
url="http://projects.vdr-developer.org/projects/plg-skinflatplus"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('graphicsmagick' "vdr-api=${_vdrapi}")
makedepends=('git')
optdepends=('nopacity-logos: To show channellogos in DisplayChannel view')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://projects.vdr-developer.org/attachments/download/1777/$_plugname-$pkgver.tar.gz"
        "50-$_plugname.conf")
backup=('etc/epgd/eventsview-flatplus.sql'
        "etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('9d67edc3415ac6b18edde390290a3c43'
         '878594e0f5af2ab308e6ab86a9bf0136')

prepare() {
  cd "$srcdir/$_plugname-$pkgver"
  sed -i 's/Magick++/GraphicsMagick++/g' Makefile
}

build() {
  cd "$srcdir/$_plugname-$pkgver"
  make
}

package() {
  cd "$srcdir/$_plugname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 contrib/eventsview-flatplus.sql "$pkgdir/etc/epgd/eventsview-flatplus.sql"
  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
