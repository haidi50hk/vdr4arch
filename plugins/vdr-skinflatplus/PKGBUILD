# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-skinflatplus
pkgver=0.3.1
_gitver=52a6afcc01e8da9ea226e15e8d4e63c5573fbb48
_vdrapi=2.1.6
pkgrel=2
pkgdesc="Simple and slim skin for VDR"
url="http://projects.vdr-developer.org/projects/plg-skinflatplus"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('graphicsmagick' "vdr-api=${_vdrapi}")
makedepends=('git')
optdepends=('nopacity-logos: To show channellogos in DisplayChannel view')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/skin-flatplus.git#commit=$_gitver"
        "50-$_plugname.conf")
backup=('etc/epgd/eventsview-flatplus.sql'
        "etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('SKIP'
         '878594e0f5af2ab308e6ab86a9bf0136')
pkgver() {
  cd "${srcdir}/skin-flatplus"
  git tag -a 0.3.1 -m 'Added Tag' 52a6afcc01e8da9ea226e15e8d4e63c5573fbb48 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

prepare() {
  cd "${srcdir}/skin-flatplus"
  sed -i 's/Magick++/GraphicsMagick++/g' Makefile
}

build() {
  cd "${srcdir}/skin-flatplus"
  make
}

package() {
  cd "${srcdir}/skin-flatplus"

  make DESTDIR="$pkgdir" install

  install -Dm644 contrib/eventsview-flatplus.sql "$pkgdir/etc/epgd/eventsview-flatplus.sql"
  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
