# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-skinnopacity
pkgver=1.0.4
_vdrapi=2.1.6
pkgrel=4
pkgdesc="highly customizable native true color skin for the Video Disc Recorder"
url="http://projects.vdr-developer.org/projects/skin-nopacity"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('graphicsmagick' "vdr-api=${_vdrapi}")
optdepends=('vdr-tvscraper: To display scraped images in various EPG views')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
backup=("var/lib/vdr/plugins/skinnopacity/themeconfigs/theme-light.conf"
        "var/lib/vdr/plugins/skinnopacity/themeconfigs/theme-default.conf"
        "var/lib/vdr/plugins/skinnopacity/themeconfigs/theme-iceblue.conf")
source=("http://projects.vdr-developer.org/attachments/download/1626/$pkgname-$pkgver.tgz"
        'nopacity-always_show_displaychannel.diff')
md5sums=('68eb23460cccf373aee97a8edfa40567'
         'c53a82c3b66a44f5b4304a01cdb66632')

prepare() {
  cd "$srcdir/$_plugname-$pkgver"
  patch -p1 -i "$srcdir/nopacity-always_show_displaychannel.diff"
}

build() {
  cd "$srcdir/$_plugname-$pkgver"
  make IMAGELIB=graphicsmagick
}

package() {
  cd "$srcdir/$_plugname-$pkgver"
  make DESTDIR="$pkgdir" install
}
