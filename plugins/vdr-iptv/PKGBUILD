pkgname=vdr-iptv
pkgver=2.1.1
_vdrapi=2.1.6
pkgrel=2
pkgdesc="Integrates multicast IPTV transport streams seamlessly into VDR"
url="http://www.saunalahti.fi/~rahrenbe/vdr/iptv/"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('curl' "vdr-api=${_vdrapi}")
optdepends=('vlc: Needed for vlc2iptv')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://www.saunalahti.fi/~rahrenbe/vdr/$_plugname/files/$pkgname-$pkgver.tgz"
        'https://github.com/yavdr/yavdr-base/raw/stable-0.5/etc/vdr/plugins/iptv/vlcinput/3Sat.conf'
        'https://github.com/yavdr/yavdr-base/raw/stable-0.5/etc/vdr/plugins/iptv/vlcinput/Bundesligen-TV.conf'
        'https://github.com/yavdr/yavdr-base/raw/stable-0.5/etc/vdr/plugins/iptv/vlcinput/Daytona-Beach.conf'
        'https://github.com/yavdr/yavdr-base/raw/stable-0.5/etc/vdr/plugins/iptv/vlcinput/NASA.conf'
        'https://github.com/yavdr/yavdr-base/raw/stable-0.5/etc/vdr/plugins/iptv/vlcinput/Sky-News.conf'
        'https://github.com/yavdr/yavdr-base/raw/stable-0.5/etc/vdr/plugins/iptv/vlcinput/THE_VOICE.conf'
        'https://github.com/yavdr/yavdr-base/raw/stable-0.5/etc/vdr/plugins/iptv/vlcinput/zdf-iptv.conf'
        'https://github.com/yavdr/yavdr-base/raw/stable-0.5/etc/vdr/plugins/iptv/vlcinput/zdf_info-iptv.conf'
        'https://github.com/yavdr/yavdr-base/raw/stable-0.5/etc/vdr/plugins/iptv/vlcinput/zdf_kultur-iptv.conf')
md5sums=('2aa1ae7677cc0353cde0d4b5f4679dd3'
         '5e960f7a71ee493c0bb7a7c56ea109c4'
         '8b51a64c8278f62dd9721a83a0654933'
         '48c790160833a16bc29aff1e0410995b'
         '7b70a8121d1ae0bc9dabc803589b89d0'
         'cbfba3010acd61c93ac6ca2b18de6c23'
         '433df5f08b933040bf81684f45006f09'
         'cf7e544dd2eba58ebef78131706749e4'
         'b611bef1267193d056f8e0b3eae3a63d'
         '793750d284f06285ea317128abcc398c')

build() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make DESTDIR="${pkgdir}" install-lib install-i18n

  install -Dm755 iptv/vlc2iptv "$pkgdir/usr/share/vdr/plugins/iptv/vlc2iptv"

  mkdir -p "$pkgdir/etc/vdr/plugins/iptv/vlcinput"
  cp "$srcdir"/*.conf "$pkgdir/etc/vdr/plugins/iptv/vlcinput"
}
