# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-play
pkgver=0.0.14_21_g8035264
_gitver=8035264d471f75407222a48f2fa707795e25d461
_vdrapi=2.1.6
pkgrel=8
pkgdesc="Mediaplayer plugin for VDR and X11"
url="http://projects.vdr-developer.org/projects/plg-play"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('AGPL3')
depends=('mplayer' "vdr-api=${_vdrapi}" 'xcb-util-image' 'xcb-util-keysyms' 'xcb-util-wm')
makedepends=('git')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/vdr-plugin-play.git#commit=$_gitver"
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('SKIP'
         '19d455b69728f7e6469763b2b2424074')

pkgver() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  git tag -a 0.0.14 -m 'Added Tag' eb4b8437553ff7666eceadd09c6cb9dd8255380e 2> /dev/null
  git describe --tags | sed 's/-/_/g'
}

build() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
