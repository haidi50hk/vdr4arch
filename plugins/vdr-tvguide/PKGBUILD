# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-tvguide
_gitrev=a65fca41d87e69007369662368ef3ac14902297b
pkgver=1.0.0_39_ga65fca4
epoch=1
_vdrapi=2.1.4
pkgrel=3
pkgdesc="highly customizable 2D EPG viewer plugin."
url="http://projects.vdr-developer.org/projects/plg-tvguide"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('graphicsmagick' 'libpng' "vdr-api=$_vdrapi")
makedepends=('git')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/vdr-plugin-tvguide.git#commit=$_gitrev"
        'tvguide-graphicsmagick.patch')
md5sums=('SKIP'
         '8e015974ed52603440da79420dc5a6b5')

pkgver() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
    git tag -a 1.0.0 -m 'Added Tag' c545f6b4cfc4c8fe604eb216fb1d8c86550e74ca 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

prepare() {
  cd "$srcdir/vdr-plugin-${_plugname}"
  patch -p1 -i "$srcdir/tvguide-graphicsmagick.patch"
}

build() {
  cd "$srcdir/vdr-plugin-${_plugname}"
  make IMAGELIB=graphicsmagick
}

package() {
  cd "$srcdir/vdr-plugin-${_plugname}"
  make DESTDIR="$pkgdir" install

#   find "$pkgdir" -name *.png -exec gm convert +profile icm {} {} \;
}
